<expression> ::= <sub-expression> | <index-expression> | <comparator-expression> | <or-expression> | <identifier> | <and-expression> | <not-expression> | <paren-expression> | "*" | <multi-select-list> | <multi-select-hash> | <literal> | <function-expression> | <pipe-expression> | <raw-string> | <current-node>
<sub-expression> ::= <expression> "." ( <identifier> | <multi-select-list> | <multi-select-hash> | <function-expression> | "*" )
<pipe-expression>  ::= <expression> "|" <expression>
<or-expression>     ::= <expression> "||" <expression>
<and-expression>    ::= <expression> "&&" <expression>
<not-expression>    ::= "!" <expression>
<paren-expression>  ::= "(" <expression> ")"
<index-expression>  ::= <expression> <bracket-specifier> | <bracket-specifier>
<multi-select-list> ::= "[" ( <expression> *( "," <expression> ) ) "]"
<multi-select-hash> ::= "{" ( <keyval-expr> *( "," <keyval-expr> ) ) "}"
<keyval-expr>       ::= <identifier> ":" <expression>
<bracket-specifier> ::= "[" (<number> | "*" | <slice-expression>) "]" | "[]" | "[?" <expression> "]"
<comparator-expression> ::= <expression> <comparator> <expression>
<slice-expression>  ::= [<number>] ":" [<number>] [ ":" [<number>] ]
<comparator>        ::= "<" | "<:=" | "==" | ">=" | ">" | "!="
<function-expression> ::= <unquoted-string>  (
                        <no-args>  |
                        <one-or-more-args> )
<no-args>             ::= "(" ")"
<one-or-more-args>    ::= "(" ( <function-arg> *( "," <function-arg> ) ) ")"
<function-arg>        ::= <expression> | <expression-type>
<current-node>        ::= "@"
<expression-type>     ::= "&" <expression>

<raw-string>        ::= "'" *<raw-string-char> "'"
<raw-string-char>   ::= (' '|'!'|'"'|'#'|'$'|'%'|'&'| '('|')'|'*'|'+'|','|'-'|'.'|'/'|'0'|'1'|'2'|'3'|'4'|'5'|'6'|'7'|'8'|'9'|':'|';'|'<'|'='|'>'|'?'|'@'|'A'|'B'|'C'|'D'|'E'|'F'|'G'|'H'|'I'|'J'|'K'|'L'|'M'|'N'|'O'|'P'|'Q'|'R'|'S'|'T'|'U'|'V'|'W'|'X'|'Y'|'Z'|'[' |']'|'^'|'_'|'`'|'a'|'b'|'c'|'d'|'e'|'f'|'g'|'h'|'i'|'j'|'k'|'l'|'m'|'n'|'o'|'p'|'q'|'r'|'s'|'t'|'u'|'v'|'w'|'x'|'y'|'z'|'{'|'|'|'}'|'~') | <preserved-escape> | <raw-string-escape>
<preserved-escape>  ::= <escape> (' '|'!'|'"'|'#'|'$'|'%'| '&'|'('|')'|'*'|'+'|','|'-'|'.'|'/'|'0'|'1'|'2'|'3'|'4'|'5'|'6'|'7'|'8'|'9'|':'|';'|'<'|'='|'>'|'?'|'@'|'A'|'B'|'C'|'D'|'E'|'F'|'G'|'H'|'I'|'J'|'K'|'L'|'M'|'N'|'O'|'P'|'Q'|'R'|'S'|'T'|'U'|'V'|'W'|'X'|'Y'|'Z'|'[' |']'|'^'|'_'|'`'|'a'|'b'|'c'|'d'|'e'|'f'|'g'|'h'|'i'|'j'|'k'|'l'|'m'|'n'|'o'|'p'|'q'|'r'|'s'|'t'|'u'|'v'|'w'|'x'|'y'|'z'|'{'|'|'|'}'|'~')
<raw-string-escape> ::= <escape> ("'" | <escape>)
<literal>           ::= "`" <json-value> "`"
<unescaped-literal> ::= ' '|'!'|'#'|'$'|'%'|'&'|'''|'('|')'|'*'|'+'|','|'-'|'.'|'/'|'0'|'1'|'2'|'3'|'4'|'5'|'6'|'7'|'8'|'9'|':'|';'|'<'|'='|'>'|'?'|'@'|'A'|'B'|'C'|'D'|'E'|'F'|'G'|'H'|'I'|'J'|'K'|'L'|'M'|'N'|'O'|'P'|'Q'|'R'|'S'|'T'|'U'|'V'|'W'|'X'|'Y'|'Z'|'[' |']'|'^'|'_'|'a'|'b'|'c'|'d'|'e'|'f'|'g'|'h'|'i'|'j'|'k'|'l'|'m'|'n'|'o'|'p'|'q'|'r'|'s'|'t'|'u'|'v'|'w'|'x'|'y'|'z'|'|'|'}'|'~'
<escaped-literal>   ::= <escaped-char> | (<escape> '`')
<number>            ::= ["-"]1*<digit>
<digit>             ::= '0'|'1'|'2'|'3'|'4'|'5'|'6'|'7'|'8'|'9'
<hexdig>	    ::=	'0'|'1'|'2'|'3'|'4'|'5'|'6'|'7'|'8'|'9'|'A'|'B'|'C'|'D'|'E'|'F'
<identifier>        ::= <unquoted-string> | <quoted-string>
<unquoted-string>   ::= ('A'|'B'|'C'|'D'|'E'|'F'|'G'|'H'|'I'|'J'|'K'|'L'|'M'|'N'|'O'|'P'|'Q'|'R'|'S'|'T'|'U'|'V'|'W'|'X'|'Y'|'Z'|'a'|'b'|'c'|'d'|'e'|'f'|'g'|'h'|'i'|'j'|'k'|'l'|'m'|'n'|'o'|'p'|'q'|'r'|'s'|'t'|'u'|'v'|'w'|'x'|'y'|'z'|'_') *('0'|'1'|'2'|'3'|'4'|'5'|'6'|'7'|'8'|'9'|A'|'B'|'C'|'D'|'E'|'F'|'G'|'H'|'I'|'J'|'K'|'L'|'M'|'N'|'O'|'P'|'Q'|'R'|'S'|'T'|'U'|'V'|'W'|'X'|'Y'|'Z'|'a'|'b'|'c'|'d'|'e'|'f'|'g'|'h'|'i'|'j'|'k'|'l'|'m'|'n'|'o'|'p'|'q'|'r'|'s'|'t'|'u'|'v'|'w'|'x'|'y'|'z'|'_')
<quoted-string>     ::= <quote> 1*(<unescaped-char> | <escaped-char>) <quote>
<unescaped-char>    ::= ' '|'!'| '#'|'$'|'%'|'&'|'''|'('|')'|'*'|'+'|','|'-'|'.'|'/'|'0'|'1'|'2'|'3'|'4'|'5'|'6'|'7'|'8'|'9'|':'|';'|'<'|'='|'>'|'?'|'@'|'A'|'B'|'C'|'D'|'E'|'F'|'G'|'H'|'I'|'J'|'K'|'L'|'M'|'N'|'O'|'P'|'Q'|'R'|'S'|'T'|'U'|'V'|'W'|'X'|'Y'|'Z'|'[' |']'|'^'|'_'|'`'|'a'|'b'|'c'|'d'|'e'|'f'|'g'|'h'|'i'|'j'|'k'|'l'|'m'|'n'|'o'|'p'|'q'|'r'|'s'|'t'|'u'|'v'|'w'|'x'|'y'|'z'|'{'|'|'|'}'|'~'
<escape>            ::= %x5C
<quote>             ::= '"'
<escaped-char>      ::= <escape> ('"'|%x5C|'/'|'b'|'f'|'n'|'r'|'t'|'u' 4<hexdig> )
<json-value> ::= <false> | <null> | <true> | <json-object> | <json-array> | <json-number> | <json-quoted-string>
<false> ::= 'false'
<null>  ::= 'null'
<true>  ::= 'true'
<json-quoted-string> ::= '"' 1*(<unescaped-literal> | <escaped-literal>) '"'
<begin-array>     ::= <ws> '[' <ws>
<begin-object>    ::= <ws> '{' <ws>
<end-array>       ::= <ws> ']' <ws>
<end-object>      ::= <ws> '}' <ws>
<name-separator>  ::= <ws> ':' <ws>
<value-separator> ::= <ws> ',' <ws>
<ws>              ::= *(' ' |
                    '	' |
                    %x0A |
                    '
'  
                   )
<json-object> ::= <begin-object> [ <member> *( <value-separator> <member> ) ] <end-object>
<member> ::= <quoted-string> <name-separator> <json-value>
<json-array> ::= <begin-array> [ <json-value> *( <value-separator> <json-value> ) ] <end-array>
<json-number> ::= [ <minus> ] <int> [ <frac> ] [ <exp> ]
<decimal-point> ::= '.'
<digit1-9> ::= '0'|'1'|'2'|'3'|'4'|'5'|'6'|'7'|'8'|'9'
<e> ::= 'e' | 'E'
<exp> ::= <e> [ <minus> | <plus> ] 1*<digit>
<frac> ::= <decimal-point> 1*<digit>
<int> ::= <zero> | ( <digit1-9> *<digit> )
<minus> ::= "-"
<plus> ::= '+'
<zero> ::= '0'
